// Some beats of "All of me" with the attempt to mix a Bachata
// rhythm into the song.
//
// This file assumes the following program configuration:
// - Note System:      International: c, d, e, ... (lower case)
// - Half Tone Symbol: # (c#, d#, e#, ...)
// - Octave Naming:    +n/-n
// - Syntax:           Mixed Case Syntax


// Definition of instruments
// 1st column: MIDI channel number
// 2nd column: program number (instrument number) like it's defined by General MIDI (or corresponding
//             shortcut)
//             Optional: bank number, separated by ',' - default: 0/0
//             Bank number 
// 3rd column: channel comment

INSTRUMENTS
	0  ACOUSTIC_GRAND_PIANO,0/0  Piano (right hand)  // bank MSB=0, bank LSB=0, program=0 (Acoustic Grand Piano)
	1  0,0/0                     Piano (left hand)   // same as channel 0 but with the program number instead of the shortcut
	5  E_BASS_PICK               Bass                // bank MSB=0, bank LSB=0, program=0 (E-Bass Pick)
	p  STANDARD,0                Standard Percussion // bank MSB=0, bank LSB=0, program=0 (standard drumkit)
	                                                 // channel 9 can also be defined with 'P' (for percussion)
END


// Definition of chords (several tones that can later be played simultaneously in the same channel)
// The CHORD keyword is followed by
// - a self-given chord name; and
// - all the notes that belong to the chord

CHORD chord_1     f     c+
CHORD chord_2     f     c#+
CHORD chord_3     d#    g#     c+
CHORD chord_4     d#    g      a#


// Speed definition:
// *:   necessary because a tempo definition is a global (channel-independent) command.
//      The * is the symbol for "global"
// tempo: Beats per Minute (tempo definition)
// 128: From now on the MIDI stream runs with 128 beats per minute.

*    tempo    128


// Macro definition for the piano part
// 1st column: channel (like defined in the INSTRUMENTS section)
// 2nd column: note or chord
// 3rd column: note length
// Right from the 3rd column: optional options.
// In this case only the velocity information for
// the according channel.

MACRO verse_1
	1     f-          1   v=110
	0     chord_1     4.  v=110
	0     chord_1     4.
	0     chord_1     4

	1     c#-         1
	0     chord_2     4.
	0     chord_2     4.
	0     chord_2     4

	1     g#-2        1
	0     chord_3     4.
	0     chord_3     4.
	0     chord_3     4

	1     d#-         1
	0     chord_4     4.
	0     chord_4     4.
	0     chord_4     4
END


// Macro definition for the bass part

MACRO bassrun_1
	5     f-     4.   v=90
	5     f-     8
	5     c-     4
	5     f-     4

	5     g-     4.
	5     g-     8
	5     b-2    4
	5     g-     4

	5     g#-    4.
	5     g#-    8
	5     c-     4
	5     g#-    4

	5     g-     4.
	5     g-     8
	5     b-2    4
	5     g-     4

END


// Macro definition for the percussion part

MACRO bongo_1
	9     bongo_high     4   v=120
	9     bongo_high     4                    // program=60 (high bongo)
	9     60             4                    // same as above but using the number
	9     bongo_low      4
END


// The actual music playing part.
// The INCLUDE statement applies a previously defined MACRO.
// Several includes run parallel.
// The * synchronizes the timers (or more precisely: current ticks) of all
// channels.

*
INCLUDE verse_1
*
INCLUDE bassrun_1
INCLUDE verse_1
*
INCLUDE verse_1
INCLUDE bassrun_1
INCLUDE bongo_1
INCLUDE bongo_1
INCLUDE bongo_1
INCLUDE bongo_1
*
INCLUDE verse_1
INCLUDE bongo_1
INCLUDE bongo_1
INCLUDE bongo_1
INCLUDE bongo_1

